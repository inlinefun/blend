import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id "org.jetbrains.kotlin.jvm" version "2.1.10"
}

version = project.mod_version
group = project.group

repositories {
    mavenCentral()
    maven {
        url = "https://maven.pkg.jetbrains.space/public/p/compose/dev"
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

    include implementation("org.greenrobot:eventbus-java:3.3.1")
    include implementation("org.jetbrains.skiko:skiko-awt:${project.skiko_version}")
    ["windows-x64", "linux-x64"].forEach {platform ->
        include implementation("org.jetbrains.skiko:skiko-awt-runtime-$platform:${project.skiko_version}")
    }

}

processResources {
    filesMatching("fabric.mod.json") {
        expand([
                "version": project.version,
                "loader_version": project.property("loader_version"),
                "minecraft_version": project.property("minecraft_version"),
                "fabric_kotlin_version": project.property("fabric_kotlin_version")
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = 21
    }
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    archiveFileName = project.name + project.version
}